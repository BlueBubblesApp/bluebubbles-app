def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 31

    lintOptions {
        checkReleaseBuilds false
        disable 'InvalidPackage'
    }

    aaptOptions {
        noCompress "tflite"
    }

    defaultConfig {
        applicationId "com.bluebubbles.messaging"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 20000000 + flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
        debug {

        }
    }

    flavorDimensions "app"

    productFlavors {
        joel {
            dimension "app"
            resValue "string", "app_name", "BlueBubbles Dev (Joel)"
            resValue "color", "ic_launcher_background", "#4c49de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.joel.fileprovider"
            applicationId "com.bluebubbles.messaging.joel"
        }
        tanay {
            dimension "app"
            resValue "string", "app_name", "BlueBubbles (Tanay Special Sauce)"
            resValue "color", "ic_launcher_background", "#4c49de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.tanay.fileprovider"
            applicationId "com.bluebubbles.messaging.tanay"
        }
        alpha {
            dimension "app"
            resValue "string", "app_name", "BlueBubbles (Alpha)"
            resValue "color", "ic_launcher_background", "#49dbde"
            resValue "string", "file_provider", "com.bluebubbles.messaging.alpha.fileprovider"
            applicationId "com.bluebubbles.messaging.alpha"
        }
        beta {
            dimension "app"
            resValue "string", "app_name", "BlueBubbles (Beta)"
            resValue "color", "ic_launcher_background", "#4990de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.beta.fileprovider"
            applicationId "com.bluebubbles.messaging.beta"
        }
        prod {
            getIsDefault().set(true)
            dimension "app"
            resValue "string", "app_name", "BlueBubbles"
            resValue "color", "ic_launcher_background", "#4990de"
            resValue "string", "file_provider", "com.bluebubbles.messaging.fileprovider"
            applicationId "com.bluebubbles.messaging"
        }
    }

    buildTypes {
        release {
            productFlavors.joel.signingConfig signingConfigs.debug
            productFlavors.tanay.signingConfig signingConfigs.debug
            productFlavors.alpha.signingConfig signingConfigs.release
            productFlavors.beta.signingConfig signingConfigs.release
            productFlavors.prod.signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
        }
    }
    sourceSets {
        main {
            java {
                srcDirs 'src/main/java', 'src/main/java/workers'
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.google.firebase:firebase-analytics:20.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core:1.7.0"
    implementation "androidx.sharetarget:sharetarget:1.2.0-rc01"

    //for location sending
    implementation "com.google.android.gms:play-services-location:18.0.0"

    // for album art color
    implementation 'androidx.palette:palette:1.0.0'

    // Add the SDK for Firebase Cloud Messaging
    implementation 'com.google.firebase:firebase-messaging:23.0.0'
    implementation 'com.google.firebase:firebase-analytics:20.0.0'
    implementation 'com.google.firebase:firebase-database:20.0.3'
    implementation 'com.google.firebase:firebase-messaging-directboot:23.0.0'
    implementation 'com.google.firebase:firebase-iid:21.1.0'

    //for json string to map
    implementation 'com.google.code.gson:gson:2.8.6'

    //for crash reporting
    // implementation 'io.sentry:sentry-android:2.2.2'

    implementation 'com.google.mlkit:smart-reply:16.2.0'
    implementation 'com.google.mlkit:entity-extraction:16.0.0-beta2'

    // for workmanager
    implementation "androidx.work:work-runtime:2.7.1"

    // Why won't it fucking compile
    implementation 'com.google.guava:guava:27.0.1-android'

}
